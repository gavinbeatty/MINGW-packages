# Maintainer: XXX

_realname=neovim
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.8
pkgrel=0
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('any')
url='https://neovim.io'
backup=('etc/xdg/nvim/sysinit.vim')
license=('custom:neovim')
depends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-jemalloc" "${MINGW_PACKAGE_PREFIX}-libtermkey" "${MINGW_PACKAGE_PREFIX}-libuv" "${MINGW_PACKAGE_PREFIX}-msgpack-c" "${MINGW_PACKAGE_PREFIX}-unibilium" "${MINGW_PACKAGE_PREFIX}-libvterm-bzr" "${MINGW_PACKAGE_PREFIX}-luajit")
makedepends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-perl" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-make" "${MINGW_PACKAGE_PREFIX}-lua-mpack" "${MINGW_PACKAGE_PREFIX}-lua-lpeg" "${MINGW_PACKAGE_PREFIX}-gperf")
optdepends=()
source=("https://github.com/neovim/neovim/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha512sums=('431d3df859b430570e3f927328b862945a15b1ac041108599670d32af6cccac9361a143fc6af27847529629a43a5e0a0afd98b1c826d4ee1e7570de3cf14f9c7')

build() {
  cmake -Hneovim-${pkgver} -Bbuild -G "MinGW Makefiles" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_BUNDLED=OFF
  cmake --build build
}

check() {
  cd "${srcdir}/build"
  ./bin/nvim --version
  ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
  cd "${srcdir}/build"
  mingw32-make install DESTDIR="${pkgdir}"

  cd "${srcdir}/neovim-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Make msys vim packages work
  mkdir -p "${pkgdir}"/etc/xdg/nvim
  echo "\" This line makes pacman-installed global msys vim packages work." > "${pkgdir}"/etc/xdg/nvim/sysinit.vim
  echo "source /usr/share/nvim/msys-packages.vim" >> "${pkgdir}"/etc/xdg/nvim/sysinit.vim

  mkdir -p "${pkgdir}"/usr/share/vim
  echo "set runtimepath+=/usr/share/vim/vimfiles" > "${pkgdir}"/usr/share/nvim/msys-packages.vim
}

# vim:set sw=2 sts=2 et:
